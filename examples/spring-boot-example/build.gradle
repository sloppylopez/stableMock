plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.stablemock' version '1.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    mavenLocal()  // For local StableMock builds
}

configurations.all {
    // CI uses Gradle dependency caching; ensure SNAPSHOT updates are picked up immediately
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {
    snapshot_testing_version = '3.2.0'
    springCloudVersion = '2024.0.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter'
    
    // Feign Client - using secure version 4.1.6 from Spring Cloud BOM
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    
    // Use local StableMock build (run './gradlew publishToMavenLocal' from parent)
    testImplementation('com.stablemock:stablemock:1.0-SNAPSHOT') {
        changing = true
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    
    // Spring Boot Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    
    // JUnit 5
    testImplementation platform('org.junit:junit-bom:5.9.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    
    // Snapshot Testing
    testImplementation "io.github.origin-energy:java-snapshot-testing-junit5:${snapshot_testing_version}"
    testImplementation "io.github.origin-energy:java-snapshot-testing-plugin-jackson:${snapshot_testing_version}"
}

test {
    useJUnitPlatform()
    systemProperty 'stablemock.mode', System.getProperty('stablemock.mode')
    systemProperty 'stablemock.showMatches', System.getProperty('stablemock.showMatches')
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

tasks.named('stableMockRecord') {
    // Reuse test sources / classpath
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    // Run recording sequentially in CI to ensure all tests execute and all mappings are recorded
    // For local development, allow parallel execution for speed
    // Parallel execution can cause race conditions or missed recordings in CI environments
    if (System.getenv('CI') != null) {
        maxParallelForks = 1
    } else {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}

tasks.named('stableMockPlayback') {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    // Run playback sequentially in CI to avoid race conditions during merge
    // When multiple test classes run in parallel, they all try to merge at the same time
    // which can cause file system sync issues or incomplete merges
    // For local development, allow parallel execution for speed
    if (System.getenv('CI') != null) {
        maxParallelForks = 1
    } else {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}

